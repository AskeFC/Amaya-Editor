! file MathML.trans : structure transformation for MathML
! see HTML.trans for a description of Trans language
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

! WARNING : transformations does NOT work with Multiscript elements
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

!surround an element with a root
root : *;
	{
	* > MROOT:*;
	* > MROOT:NONE;
	}

!surround an element with a square root
square root: *;
        {
        * > MSQRT:*;
        }

!surround a sequence of elements with a root
root: *,*+,last:*;
        {
        * > MROOT.MROW:*;
	last > MROOT.MROW:*;
	last > MROOT:NONE;
        }

!surround a sequence of elements with a square root
square root: *,*+;
        {
        * > MSQRT.MROW:*;
        }

!remove a square root
remove square root: MSQRT{*+};
        {
        * > :*;
        }

!transform 2 elements into a fraction
fraction: (num:*,den:*) | MROW{num:*,den:*};
        {
        num > MFRAC:*;
        den > MFRAC:*;
        }

!transform an element into a numerator
numerator: *;
        {
        * > MFRAC:*;
        * > MFRAC:NONE;
        }

!transform an sequence of elements into a numerator
numerator: *,*+,last:*;
        {
        * > MFRAC.MROW:*;
	last > MFRAC.MROW:*;
        last > MFRAC:NONE;
        }

!transform an element into a denominator
denominator: *;
        {
        *> MFRAC:NONE;
        *> MFRAC:*;
        }

!transform an sequence of elements into a denominator
denominator: first:*,*+;
        {
        first > MFRAC:NONE;
	first > MFRAC.MROW:*;
        * > MFRAC.MROW:*;
        }

!remove a fraction, replacing it by mrow
remove fraction: MFRAC{(num:*,den:*)|numalone:*};
        {
        num > MROW:*;
        den > MROW:*;
        numalone > :*;
        }


!surroud an element with a subsup
add sub and sup:*;
	{
	* > MSUBSUP:*;
	* > MSUBSUP:NONE;
	* > MSUBSUP:NONE;
	}

!surroud element with a sub
add subscript:*;
	{
	* > MSUB:*;
	* > MSUB:NONE;
	}

!surroud an element with a sup
add superscript:*;
	{
	* > MSUP:*;
	* > MSUP:NONE;
	}

!surroud an element with a underover
add under and over:*;
	{
	* > MUNDEROVER:*;
	* > MUNDEROVER:NONE;
	* > MUNDEROVER:NONE;
	}

!surroud an element with a over
add over:*;
	{
	* > MOVER:*;
	* > MOVER:NONE;
	}

!surroud an element with a under
add under:*;
	{
	* > MUNDER:*;
	* > MUNDER:NONE;
	}

!change MSUB into MSUP
superscript:MSUB;
	{
	MSUB > MSUP;
	}

!change MSUP into MSUB
subscript:MSUP;
	{
	MSUP > MSUB;
	}

!change MSUB or MSUP into MSUBSUP
subsup:MSUB{base:*,sub:*} | MSUP{base:*,sup:*};
	{
	base > MSUBSUP:*;
	sup > MSUBSUP:NONE;
	sup > MSUBSUP:*;
	sub > MSUBSUP:*;
	sub > MSUBSUP:NONE;
	}


! remove a Row level
remove row: MROW{*+};
        {
        * > :*;
        }

!surround an element with parentheses
parenthesize:*;
        {
        * > MROW:MF."(";
        * > MROW:*;
        * > MROW:MF.")";
        }

!surround a sequence of elements with parentheses
parenthesize: first:*,?*, last:* ;
        {
        first > MROW:MF."(";
        first > MROW:*;
        * > MROW:*;
        last > MROW:*;
        last > MROW:MF.")";
        }
