GRAMMAR OF FILE STRUCT.LAN


 rule
number

		{ This grammar defines the language S of Thot
		  The notation used here is defined in file META.LAN
	
		   ' '	: keyword
		   [ ]	: optional part
		   < >	: optional and repeatable part
		   /	: choice
	
		  V. Quint	March 1985	}
	
	
    1	StructModel =	   'STRUCTURE' [ 'EXTENSION' ] ElemName ';'
			   'DEFPRES' PresentName ';'
			 [ 'ATTR' AttrList ]
			 [ 'CONST' ConstList ]
			 [ 'STRUCT' RuleList ]
			 [ 'EXTENS' ExtensList ]
			 [ 'UNITS' RuleList ]
			 [ 'EXPORT' ExpList ]
			 [ 'EXCEPT' ExceptList ]
			   'END' .
	
    3	PresentName =	 NAME .
	
    4	AttrList =	 Attribute < Attribute > .
   10	Attribute =	 AttrName '=' AttrType ';' .
   12	AttrType =	 'INTEGER' / 'TEXT' /
			 'REFERENCE' '(' TypeRef ')' /
			 ValueList .
   13	TypeRef =	 'ANY' / [ FirstSec ] ElemName [ ExtStruct ] .
   15	FirstSec =       'First' / 'Second' .
   14	ValueList =	 AttrValue < ',' AttrValue > .
	
    5	ConstList =	 Const < Const > .
	
   18	Const =		 ConstName '=' ConstValue ';' .
	
   19	ConstName =	 NAME .
	
   20	ConstValue =	 STRING .
	
   21	BasicType =	 'TEXT' / 'GRAPHICS' / 'SYMBOL' / 'PICTURE' / 'UNIT' /
			 'NATURE' .
	
    6	RuleList =	 Rule ';' < Rule ';' > .
   22	Rule =		 ElemName [ LocAttrList ] '=' DefWith .
	
    2	ElemName =	 NAME .
	
   23	LocAttrList =	 '(' 'ATTR' LocalAttr < ';' LocalAttr > ')' .
   25	LocalAttr = 	 [ '!' ] AttrName [ '=' AttrType ] .
	
   24	DefWith =	 Definition
			 [ '+' '(' InclList ')' ] [ '-' '(' ExclList ')' ]
			 [ 'WITH' FixedAttrList ] .
	
   27	InclList =	 InclElem < ',' InclElem > .
   30	InclElem =       ElemName / BasicType .
   28	ExclList =	 ExclElem < ',' ExclElem > .
   31	ExclElem =       ElemName / BasicType .
	
   26	Definition =	 BasicType [ LocAttrList ]  / Constr / Element .
   33	Element =	 ElemName [ ExtOrDef ] .
   34	ExtOrDef =	 'EXTERN' / 'INCLUDED' / [ LocAttrList ] '=' Definition .
	
   32	Constr =	 'LIST' [ '[' min '.' '.' max ']' ] 'OF' '(' DefWith ')' /
			 'BEGIN' OptDefList 'END' /
			 'AGGREGATE' OptDefList 'END' /
			 'CASE' 'OF' DefList 'END' /
			 'REFERENCE' '(' TypeRef ')' /
			 'CONSTANT' ConstName /
			 'PAIR' / 'ANY' .
	
   37	OptDefList =	 OptDefWith ';' < OptDefWith ';' > .
	
   39	OptDefWith =	 [ '?' ] DefWith .
	
   38	DefList =	 DefWith ';' < DefWith ';' > .
	
   35	min =		 Integer / '*' .
	
   36	max =		 Integer / '*' .
	
   40	Integer =	 NUMBER .
	
   29	FixedAttrList =	 FixedAttr < ',' FixedAttr > .
   41	FixedAttr =	 AttrName [ FixModValue ] .
	
   42	FixModValue =	 ['?'] '=' FixedValue .
   43	FixedValue =	 [ '-' ] NumValue / StrValue / AttrValue .
	
   11	AttrName =	 NAME .
	
   17	AttrValue =	 NAME .
	
   44	NumValue =	 NUMBER .
	
   45	StrValue =	 STRING .
	
   16	ExtStruct =	 '(' ElemName ')' .
	
    8	ExpList =	 Export < ',' Export > ';' .
	
   46	Export =	 ElemName [ 'WITH' Content ] .
	
   47	Content =	 'Nothing' / ElemName [ ExtStruct ] .
	
    9	ExceptList =	 Except ';' < Except ';' > .
   48	Except =	 ExceptType ':' ExValList .
   49	ExceptType =	 BasicType / [ 'EXTERN' ] [ FirstSec ] ElemName / AttrName / ConstName .
   50	ExValList =	 ExceptVal < ',' ExceptVal > .
   51	ExceptVal =	 'NoCut' / 'CanCut' / 'NoCreate' / 'NoReplicate' /
	     'NoHMove' / 'NoVMove' / 'NoMove' /
			 'NoHResize' / 'NoVResize' / 'NoResize' /
	     'MoveResize' /
			 'NewWidth' / 'NewHeight' / 'NewPercentWidth' /
			 'NewHPos' / 'NewVPos' /
	     'Invisible' / 'Hidden' /
		   'PageBreak' / 'PageBreakAllowed' / 'PageBreakPlace' /
		   'PageBreakRepetition' / 'PageBreakRepBefore' /
	     'NoPaginate' / 'NoSpellCheck' /
			 'ActiveRef' /  'NoSelect' /
	     'HighlightChildren' / 'ExtendedSelection' /
	     'SelectParent' / 'ClickableSurface' /
			 'ParagraphBreak' / 'IsBreak' /
			 'ReturnCreateNL' / 'ReturnCreateWithin' / 'NoBreakByReturn' /
	     'ImportLine' / 'ImportParagraph' /
			 'GraphCreation' / 'IsDraw' / 'IsGroup' / 'EmptyGraphic' /
			 'IsTable' / 'IsRow' / 'IsColHead' / 'IsCell' / 'IsCaption' /
			 'ColRef' / 'ColSpan' / 'RowSpan' /
			 'CssBackground' / 'CssClass' / 'CssId' / 'CssPseudoClass' /
			 'NoShowBox' / 'SetWindowBackground' /
			 'Shadow' / 'EventAttr' / 'SpacePreserve' /
			 'IsPlaceholder' / 'StartCounter' / 'SetCounter' /
			 'MarkupPreserve' / 'NotAnElementNode' / 'CheckAllChars' /
	     'IsImg' / 'IsMap' / 'DuplicateAttr' / 'PseudoParag' /
			 ExceptNum .
